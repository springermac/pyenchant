# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=enchant
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.0
pkgrel=8
pkgdesc="Enchanting Spell Checking Library (mingw-w64)"
arch=('any')
url="http://www.abisource.com/"
license=(LGPL2.1)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
         ${MINGW_PACKAGE_PREFIX}-glib2
         ${MINGW_PACKAGE_PREFIX}-hunspell)
options=('!libtool' 'strip' '!debug' '!makeflags')
source=("http://www.abisource.com/downloads/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.gz"
        01-not-use-hardcoded-paths.patch
        relocatable.patch)
md5sums=('de11011aff801dc61042828041fb59c7'
         '4fc6176b94518ac1307e4a2173dc64d4'
         '179ebabab2bfaec6e62bc2cd57d1ad54')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # Use instead relocate patch
  patch -p1 -i ${srcdir}/01-not-use-hardcoded-paths.patch
  # PyEnchant patch
  patch -Np0 -i "${srcdir}/relocatable.patch"

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-ispell \
    --enable-myspell \
    --disable-aspell \
    --with-system-myspell \
    --with-aspell-prefix=${MINGW_PREFIX} \
    --disable-voikko \
    --disable-uspell \
    --disable-hspell \
    --disable-zemberek \
    --enable-static \
    --disable-shared

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LIB" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB"
}
